buildscript {
	project.ext {
        bootVersion = '1.2.3.RELEASE'
		cloudVersion = '1.0.0.RELEASE'
		seurityVersion = '2.0.7.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${project.bootVersion}"
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'java'
apply plugin: 'docker'

jar {
    baseName = 'sample-api-gateway'
    version =  '0.0.1'
}

repositories { 
	mavenCentral() 
	jcenter()
}
dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:${project.bootVersion}")    
    compile("org.springframework.boot:spring-boot-starter-actuator:${project.bootVersion}")
	compile("org.springframework.boot:spring-boot-starter-data-jpa:${project.bootVersion}")
	compile("mysql:mysql-connector-java:5.1.30")
	compile("org.springframework.cloud:spring-cloud-config-client:${project.cloudVersion}")
	compile("org.springframework.cloud:spring-cloud-starter-eureka:${project.cloudVersion}")
	compile("org.springframework.cloud:spring-cloud-starter-zuul:${project.cloudVersion}")
	compile("org.springframework.cloud:spring-cloud-starter-security:${project.cloudVersion}")
	
	compile("org.springframework.boot:spring-boot-starter-security:${project.bootVersion}")
	compile("org.springframework.security.oauth:spring-security-oauth2:${project.seurityVersion}")
	
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile 'org.springframework:spring-test:4.0.6.RELEASE'
}

/**
* These values(group & mainClassName) are required by the gradle docker plugin.
*
* The "group" value feeds into the docker tag and is required if you want to push the images
* to docker hub.
*
* The "mainClassName" value tells which class has the "main" entry point for running the 
* Spring boot application.
*/
group = 'anilallewar'
mainClassName = 'com.rohitghatol.microservice.gateway.Application'

sourceCompatibility = 1.7
targetCompatibility = 1.7

distDocker {
    exposePort 8080
    setEnvironment 'JAVA_OPTS', '-Dspring.profiles.active=docker'
}

/**
* On Mac, docker can't be connected locally since it is running in a separate VM.  
*
* NOTE: If you have not added your TLS certs to boot2docker; you would need to change 
* docker to run the API on HTTP; while boot2docker 1.3 comes with TLS enabled. Hence 
* you need to run the following command "$(docker run sequenceiq/socat)" at the docker
* prompt so that this image maps the api to HTTP port. You can check that it is working
* correctly using the command "curl http://192.168.59.103:2375/_ping"
*
*
* In order to change docker to run the remote API, we need to set the following flags:
* 1. useApi true => Use the Docker Remote API instead of a locally installed docker binary.
* 2. hostUrl => set the URL used to contact the Docker server. Defaults to http://localhost:2375
*/
docker {
    useApi true
    hostUrl 'http://192.168.59.103:2375'
    baseImage = 'java:7'
}

bootRun {
    jvmArgs = ['-Xdebug', '-Xrunjdwp:server=y,transport=dt_socket,address=4000,suspend=n','-Dspring.profiles.active=dev']
}

run {
    jvmArgs = ['-Xdebug', '-Xrunjdwp:server=y,transport=dt_socket,address=4000,suspend=n','-Dspring.profiles.active=dev']
}


task createWrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

